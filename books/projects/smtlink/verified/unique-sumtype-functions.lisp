;; Copyright (C) 2015, University of British Columbia
;; Written by Yan Peng (Feb 22nd 2022)
;;
;; License: A 3-clause BSD license.
;; See the LICENSE file distributed with ACL2
;;

(in-package "SMT")
(include-book "std/util/bstar" :dir :system)
(include-book "std/util/define" :dir :system)

(include-book "hint-interface")

(define unique-destructor-list ((des-lst smt-function-list-p)
                                (fn-acc symbol-listp)
                                (trans-acc string-listp))
  :measure (len des-lst)
  :returns (mv (fn-lst symbol-listp) (trans-lst string-listp))
  (b* ((des-lst (smt-function-list-fix des-lst))
       (fn-acc (symbol-list-fix fn-acc))
       (trans-acc (string-list-fix trans-acc))
       ((unless (consp des-lst)) (mv fn-acc trans-acc))
       ((cons des-hd des-tl) des-lst)
       ((smt-function df) des-hd)
       ((trans-hint th) df.translation-hint))
    (unique-destructor-list des-tl
                            (cons df.name fn-acc)
                            (cons th.translation trans-acc))))

(define unique-function-sum ((sum smt-sum-p)
                             (fn-acc symbol-listp)
                             (trans-acc string-listp))
  :returns (mv (fn-lst symbol-listp) (trans-lst string-listp))
  (b* ((sum (smt-sum-fix sum))
       (fn-acc (symbol-list-fix fn-acc))
       (trans-acc (string-list-fix trans-acc))
       ((smt-sum s) sum)
       ((smt-function cf) s.constructor)
       ((mv new-fn new-trans)
        (unique-destructor-list s.destructors fn-acc trans-acc)))
    (mv (cons cf.name new-fn) new-trans)))

(define unique-function-sum-list ((sums smt-sum-list-p)
                                  (fn-acc symbol-listp)
                                  (trans-acc string-listp))
  :measure (len sums)
  :returns (mv (fn-lst symbol-listp)
               (trans-lst string-listp))
  (b* ((sums (smt-sum-list-fix sums))
       (fn-acc (symbol-list-fix fn-acc))
       (trans-acc (string-list-fix trans-acc))
       ((unless (consp sums)) (mv fn-acc trans-acc))
       ((cons s-hd s-tl) sums)
       ((mv fn-hd trans-hd) (unique-function-sum s-hd fn-acc trans-acc)))
    (unique-function-sum-list s-tl fn-hd trans-hd)))

(define unique-sumtype-function ((type smt-datatype-p))
  :returns (type maybe-smt-datatype-p)
  (b* ((type (smt-datatype-fix type))
       ((unless (equal (smt-datatype-kind type) :sumtype)) nil)
       (sums (smt-datatype-sumtype->sums type))
       ((mv fn-lst trans-lst) (unique-function-sum-list sums nil nil))
       (okp1 (no-duplicatesp-equal fn-lst))
       (okp2 (no-duplicatesp-equal trans-lst)))
    (if (and okp1 okp2) nil type)))

(define unique-sumtype-function-list ((types smt-datatype-list-p))
  :returns (type maybe-smt-datatype-p)
  :measure (len types)
  (b* ((types (smt-datatype-list-fix types))
       ((unless (consp types)) nil)
       ((cons type-hd type-tl) types)
       (not-unique? (unique-sumtype-function type-hd))
       ((if not-unique?) not-unique?))
    (unique-sumtype-function-list type-tl)))
